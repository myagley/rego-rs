use ordered_float::NotNan;

use crate::Location;
use crate::parser::lexer::Error;
use crate::parser::token::{StringLiteral, Token};
use crate::parser::tree::*;
use crate::value::{Number, Value};

grammar<'input>(input: &'input str);

extern {
    type Location = Location;
    type Error = Error;

    enum Token<'input> {
        "as" => Token::As,
        "default" => Token::Default,
        "else" => Token::Else,
        "false" => Token::False,
        "import" => Token::Import,
        "not" => Token::Not,
        "null" => Token::Null,
        "package" => Token::Package,
        "set" => Token::Set,
        "some" => Token::Some,
        "true" => Token::True,
        "with" => Token::With,

        "integer-literal" => Token::IntLiteral(<i64>),
        "float-literal" => Token::FloatLiteral(<NotNan<f64>>),
        "string-literal" => Token::StringLiteral(<StringLiteral<'input>>),

        "id" => Token::Identifier(<&'input str>),

        "=" => Token::Equal,
        "==" => Token::EqualEqual,
        "!=" => Token::NotEqual,
        "<" => Token::Less,
        "<=" => Token::LessEqual,
        ">" => Token::Greater,
        ">=" => Token::GreaterEqual,
        "+" => Token::Plus,
        "-" => Token::Minus,
        "*" => Token::Star,
        "/" => Token::Slash,
        "&" => Token::Amper,
        "|" => Token::Vbar,

        "(" => Token::ParenOpen,
        ")" => Token::ParenClose,
        "[" => Token::BracketOpen,
        "]" => Token::BracketClose,
        "{" => Token::BraceOpen,
        "}" => Token::BraceClose,

        ";" => Token::SemiColon,
        ":" => Token::Colon,
        "," => Token::Comma,
        "." => Token::Dot,
        "_" => Token::UnderScore,
        ":=" => Token::Assign,
    }
}

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Dot<T>: Vec<T> = {
    <v:(<T> ".")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

pub Module: Module<'input> = Package Import* Rule* => Module::new(<>);

Import: Import<'input> = "import" <Ref> <("as" <Var>)?> ";"+ => Import::new(<>);

Package: Vec<&'input str> = "package" <Dot<"id">> ";"+;

Rule: Rule<'input> = {
    "default" <Var> "=" <Term> ";"+ => Rule::Default(DefaultRule::new(<>)),
    <Var> <( ExprOp <Term> )?> <RuleBodyElse?> ";"+ => Rule::Complete(CompleteRule::new(<>)),
    <Var> "[" <Term> "]" <RuleBody?> ";"+ => Rule::Set(SetRule::new(<>)),
    <Var> "[" <Term> "]" "=" <Term> <RuleBody?> ";"+ => Rule::Object(ObjectRule::new(<>)),
    <Var> "(" <Comma<Term>> ")" "=" <Term> <RuleBodyElse?> ";"+ => Rule::Function(FunctionRule::new(<>)),
};

RuleBody: RuleBody<'input> = {
    "{" <Query> "}" => RuleBody::Clauses(vec![<>]),
    "{" <h:Query> "}" <t:("{" <Query> "}")+> => {
        let mut clauses = vec![h];
        let mut tail = t;
        clauses.append(&mut tail);
        RuleBody::Clauses(clauses)
    },
};

RuleBodyElse: RuleBody<'input> = {
    "{" <Query> "}" => RuleBody::Clauses(vec![<>]),
    "{" <h:Query> "}" <t:("{" <Query> "}")+> => {
        let mut clauses = vec![h];
        let mut tail = t;
        clauses.append(&mut tail);
        RuleBody::Clauses(clauses)
    },
    "{" <Query> "}" <(<ElseClause>)+> => RuleBody::WithElses(<>),
};

ElseClause: Else<'input> = {
    "else" <("=" <Term>)?> "{" <Query> "}" => Else::new(<>),
};

pub Query: Query<'input> = {
    <h:Statement> <t:( ";"+ <Statement> )*> ";"? => {
        let mut t = t;
        t.reverse();
        t.push(h);
        t.reverse();
        Query::new(t)
    }
};

Statement: Statement<'input>  = {
    <StatementTarget> <WithModifier*> => Statement::new(<>),
}

StatementTarget: StatementTarget<'input> = {
    Expr => StatementTarget::Expr(<>),
    "not" <Expr> => StatementTarget::NotExpr(<>),
    "some" <Comma<Var>> => StatementTarget::Some(<>),
};

WithModifier: With<'input> = {
    "with" <Term> "as" <Term> => With::new(<>),
};

Tier<Op, NextTier>: Term<'input> = {
    <l:Tier<Op, NextTier>> <op:Op> <r:NextTier> => Term::BinOp(Box::new(l), op, Box::new(r)),
    NextTier,
};

Expr = Tier<ExprOp, Tier0>;
Tier0 = Tier<Tier0Op, Tier1>;
Tier1 = Tier<Tier1Op, Tier2>;
Tier2 = Tier<Tier2Op, Tier3>;
Tier3 = Tier<Tier3Op, Term>;

ExprOp: Opcode = {
    ":=" => Opcode::Assign,
    "=" => Opcode::Eq,
}

Tier3Op: Opcode = {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
    "&" => Opcode::And,
    "|" => Opcode::Or,
};

Tier2Op: Opcode = {
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
};

Tier1Op: Opcode = {
    "<" => Opcode::Lt,
    "<=" => Opcode::Lte,
    ">" => Opcode::Gt,
    ">=" => Opcode::Gte,
};

Tier0Op: Opcode = {
    "==" => Opcode::EqEq,
    "!=" => Opcode::Ne,
};

Ref: Ref<'input> = {
    <t:RefTarget> <a:RefArg*> => Ref::new(Box::new(t), a),
};

#[inline]
RefTarget: RefTarget<'input> = {
    Var => RefTarget::Var(<>),
    Array => RefTarget::Collection(<>),
    ArrayCompr => RefTarget::ArrayCompr(<>),
    Object => RefTarget::Collection(<>),
    ObjectCompr => RefTarget::ObjectCompr(<>),
    Set => RefTarget::Collection(<>),
    SetCompr => RefTarget::SetCompr(<>),
    ExprCall => RefTarget::ExprCall(<>),
};

#[inline]
RefArg: RefArg<'input> = {
    RefArgDot,
    RefArgBrack,
};

#[inline]
RefArgBrack: RefArg<'input> = {
    "[" <RefArgBrackParam> "]"
};

#[inline]
RefArgBrackParam: RefArg<'input> = {
    Scalar => RefArg::Scalar(<>),
    Var => RefArg::Var(<>),
    Array => RefArg::Collection(<>),
    Object => RefArg::Collection(<>),
    Set => RefArg::Collection(<>),
    "_" => RefArg::Anon,
};

#[inline]
RefArgDot: RefArg<'input> = {
    "." <Var> => RefArg::Scalar(Value::String(<>.to_string())),
};

// There is some ambiguity in the grammar
// The left hand side should be Var ("." Var)*.
// This conflicts with Ref
ExprCall: ExprCall<'input> = {
    <r:Ref> "(" <t:Comma<Term>> ")" => ExprCall::new(r, t),
};

Term: Term<'input> = {
    Ref => Term::Ref(<>),
    Scalar => Term::Scalar(<>),
    "(" <Tier0> ")",
};

ArrayCompr: ArrayCompr<'input> = "[" <Term>  "|" <Query> "]" => ArrayCompr::new(<>);

SetCompr: SetCompr<'input> = "{" <Term> "|" <Query> "}" => SetCompr::new(<>);

ObjectCompr: ObjectCompr<'input> = "{" <ObjectItem> "|" <Query> "}" => ObjectCompr::new(<>);

Array: Collection<'input> = "["  <Comma<Term>> "]" => Collection::Array(<>);

Set: Collection<'input> = {
    NonEmptySet,
    EmptySet,
};

NonEmptySet: Collection<'input> = {
    "{" <v:(<Term> ",")*> <e:Term> "}" => {
        let mut v = v;
        v.push(e);
        Collection::Set(v)
    }
}

EmptySet: Collection<'input> = "set" "(" ")" => Collection::Set(vec![]);

Object: Collection<'input> = "{" <Comma<ObjectItem>> "}" => Collection::Object(<>);

ObjectItem: (Term<'input>, Term<'input>) = <k:ObjectKey> ":" <v:Term> => (k, v);

#[inline]
ObjectKey: Term<'input> = {
    Scalar => Term::Scalar(<>),
    Ref => Term::Ref(<>),
};

Var: &'input str = "id";

Scalar: Value = {
    Bool,
    Null,
    Number,
    String,
};

#[inline]
Null: Value = {
    "null" => Value::Null,
};

#[inline]
Bool: Value = {
    "true" => Value::Bool(true),
    "false" => Value::Bool(false),
};

#[inline]
String: Value = {
    "string-literal" => Value::String(<>.unescape()),
};

#[inline]
Number: Value = {
    "integer-literal" => Value::Number(Number::from(<>)),
    "float-literal" => Value::Number(Number::from(<>)),
};
